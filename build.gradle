apply plugin: "java"
apply plugin: "war"

// TODO Must compile with JDK 1.7 http://www.draconianoverlord.com/2014/04/01/jdk-compatibility.html
sourceCompatibility = 1.7
version = projectVersion

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.1'
    }
}

repositories {
    jcenter();
    mavenCentral()

    maven {
        url "https://pkgs.dev.azure.com/OpenRemote/OpenRemote/_packaging/OpenRemote/maven/v1"
    }
}

dependencies {
    compile "org.openremote:or-object-model:0.2.1"

    compile "net.sf.flexjson:flexjson:$flexjsonVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.glassfish.jersey.containers:jersey-container-servlet-core:$jerseyVersion"
    compile "javax.ws.rs:javax.ws.rs-api:$jaxrsVersion"

    compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"

    compile "org.hibernate:hibernate-core:$hibernateVersion"
    compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
    compile "org.hibernate:hibernate-c3p0:$hibernateVersion"

    compile "ch.qos.logback:logback-classic:$logbackVersion"

    providedCompile "javax.servlet:javax.servlet-api:$servletVersion"

}

apply plugin: "com.bmuschko.docker-remote-api"

docker {
    url = "$System.env.DOCKER_HOST".replace("tcp:", "https:")
    certPath = new File("$System.env.DOCKER_CERT_PATH")
}


import com.bmuschko.gradle.docker.tasks.image.*

task createDockerFile(type:Dockerfile, dependsOn: build) {
    destFile = project.file("$project.buildDir/install/Dockerfile")
    from "openremote/tomcat-base"
    addFile projectDir.toPath().relativize(war.archivePath.toPath()).toString(), "webapps/"
}

task buildImage(type: DockerBuildImage, dependsOn: createDockerFile) {
    inputDir = projectDir
    dockerFile = createDockerFile.destFile
    tag = 'openremote/beehive-device-discovery'
}
